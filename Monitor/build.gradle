buildscript {
    repositories {
        jcenter()
        maven { url "https://maven.google.com" }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.0'
    }
}

apply plugin: 'com.android.application'

android {
    compileSdkVersion 22
    buildToolsVersion "26.0.2"

    defaultConfig {
        applicationId "com.orange.atk.monitor"
        minSdkVersion 15
        targetSdkVersion 21
        versionCode 7
        versionName '2.9'
    }

    lintOptions {
        disable 'LongLogTag'
    }
}

repositories {
    jcenter()
}

dependencies {
    compile 'com.jaredrummler:android-processes:1.0.4'
}

def appName
//Check if an applicationName property is supplied; if not use the name of the parent project.
if (project.hasProperty("applicationName")) {
    appName = applicationName
} else {
    appName = name
}

android.applicationVariants.all { variant ->
    if (variant.buildType.name == 'debug') {
        variant.outputs.each { output ->
            def newApkName
            //If there's no ZipAlign task it means that our artifact will be unaligned and we need to mark it as such.
            if (output.zipAlign) {
                //newApkName = "${appName}-${output.baseName}-${variant.versionName}.apk"
                newApkName = "ATK${appName}.apk"
            } else {
                newApkName = "${appName}-${output.baseName}-${variant.versionName}-unaligned.apk"
            }
            output.outputFile = new File(output.outputFile.parent, newApkName)
            output.assemble << {
                println "Copying APK inside GUI"
                copy {
                    from output.outputFile.path
                    into new File(project(':gui').projectDir.absolutePath+"/AndroidTools")
                }
            }
        }
    }
}
