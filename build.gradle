apply plugin: 'java'
apply plugin: 'groovy'
apply plugin:'application'
version = '2.2.2'
archivesBaseName = 'atk'
mainClassName = "com.orange.atk.launcher.LaunchGUIJATK"

def nsisPath = "C:\\DevProgramFiles\\NSIS"
def launch4jDir = new File(rootDir, 'installer/win')
def launch4jVersion = '3.1.0-beta2-win32'

def explodedDistDir = new File(distsDir, 'exploded')

sourceSets {
    main {
        java {
            srcDir 'src'
            exclude '**/tests/**'
        }
        resources {
            srcDir 'src'
        }
    }
    androidPlugin {
        java {
            srcDir 'src'
            include 'com/orange/atk/phone/android/**'
        }
    }
}

repositories{
    flatDir name: 'lib', dirs:"$launch4jDir"
}

configurations {
    launch4j
    nsis
}
dependencies {
    compile fileTree(dir: 'libs', include: '*.jar')
    launch4j ":launch4j:$launch4jVersion@zip"
    nsis files('installer/win/nsisant-1.3.jar')
}

task androidPluginJar(type: Jar, dependsOn: compileJava) {
    manifest {
        attributes firstKey: 'firstValue', secondKey: 'secondValue'
    }
    archiveName = 'android.jar'
    from sourceSets.main.output.classesDir
    include('com/orange/atk/phone/android/**')
}

jar {
    dependsOn classes
    from sourceSets.main.output
    exclude 'com/orange/atk/phone/android/**'
}

task getInstallerResources(type: Copy) {
    from('libs'){
        into 'lib'
        exclude 'forget_*'
    }
    from('ConfigFiles'){
        into 'ConfigFiles'
    }
    from('docs'){
        into 'docs'
    }

    from('res'){
        into 'res'
    }
    from('AndroidTools'){
        into 'AndroidTools'
        exclude 'ATKGetViewAPKWithRobotium'
        exclude 'ATKGetViewAPKWithRobotium_Source'
        exclude 'ATKMonitor'
        exclude 'ATKServiceSendEventToSolo'
        exclude 'ATKTestingAPKWithRobotium'
        exclude 'ATKTestingAPKWithRobotium_Source'
        exclude 'ATKWizard'
    }
    from(projectDir){
        include 'config.properties'
        include 'LICENSE.txt'
        include 'NOTICE.txt'
    }
    from('installer'){
        include 'log4j.xml'
    }
    into "$explodedDistDir"
}


task release (dependsOn: [jar,androidPluginJar,getInstallerResources]) << {
    copy {
        from(jar.archivePath){
            into 'lib'
        }
        from("$buildDir/libs"){
            include 'android.jar'
            into 'plugin'
        }
        into explodedDistDir
    }
    def distToolDir = new File(distsDir, 'tools')

    File distLaunch4jToolDir = new File(distToolDir, 'launch4j')
    distLaunch4jToolDir.mkdirs()
    ant {
        logger.info('Generate lib dir')

        configurations.launch4j.files.each {File file ->
            copy(file: file, todir: distLaunch4jToolDir)
        }
        logger.info('Generate launch4j windows exe.')
        String launch4jDistName = "launch4j-" + launch4jVersion
        unzip(src: "$distLaunch4jToolDir/launch4j-${launch4jVersion}.zip", dest: "$distLaunch4jToolDir/$launch4jDistName")
        try {
            ant.taskdef(
                    name: "launch4j",
                    classname: "net.sf.launch4j.ant.Launch4jTask",
                    classpath: "$distLaunch4jToolDir/$launch4jDistName/launch4j/launch4j.jar:$distLaunch4jToolDir/$launch4jDistName/launch4j/lib/xstream.jar")
            ant.launch4j() {
                config(headerType: "gui", outfile: "$explodedDistDir/atk.exe",
                        chdir:".",icon:"res/atk.ico",
                        dontWrapJar: "true" ) {
                    classPath(mainClass:"com.orange.atk.launcher.LaunchGUIJATK"){
                        cp("./lib/*")
                        cp("./plugin/*")
                    }
                    jre(minVersion: "1.7.0", jdkPreference: 'preferJdk')
                }
            }
            ant.launch4j() {
                config(headerType: "gui", outfile: "$explodedDistDir/analyser.exe",
                        chdir:".",icon:"res/analyser.ico",
                        dontWrapJar: "true" ) {
                    classPath(mainClass:"com.orange.atk.graphAnalyser.LectureJATKResult"){
                        cp("./lib/*")
                        cp("./plugin/*")
                    }
                    jre(minVersion: "1.7.0", jdkPreference: 'preferJdk')
                }
            }
            ant.launch4j() {
                config(headerType: "gui", outfile: "$explodedDistDir/screencmp.exe",
                        chdir:".",icon:"res/screencmp.ico",
                        dontWrapJar: "true" ) {
                    classPath(mainClass:"com.orange.atk.compUI.launchScreenShotComparison"){
                        cp("./lib/*")
                        cp("./plugin/*")
                    }
                    jre(minVersion: "1.7.0", jdkPreference: 'preferJdk')
                }
            }
            ant.launch4j() {
                config(headerType: "gui", outfile: "$explodedDistDir/scriptrecorder.exe",
                        chdir:".",icon:"res/scriptrecorder.ico",
                        dontWrapJar: "true" ) {
                    classPath(mainClass:"com.orange.atk.scriptRecorder.LaunchScriptRecorder"){
                        cp("./lib/*")
                        cp("./plugin/*")
                    }
                    jre(minVersion: "1.7.0", jdkPreference: 'preferJdk')
                }
            }
        } catch (Exception e) {
            logger.warn("Could not run Launch4j")
            logger.warn(e.getMessage());
        }
    }
    try{
        copy {
            from 'installer/win/nsis.nsi'
            from 'installer/win/setup-ARO-4.0.exe'
            into "$explodedDistDir"
        }
        ant.taskdef(name: "nsis",
                classname: "com.danielreese.nsisant.Task",
                classpath: configurations.nsis.asPath)
        ant.nsis(script: "$explodedDistDir/nsis.nsi", path: nsisPath) {
            define(name: "VERSION", value: "${version}")
            define(name: "RELEASE_DIR", value: "$distsDir")
        }
    }catch (Exception e) {
        logger.warn("Could not run NSIS.")
        logger.warn(e.getMessage());
    }
}